plugins {
    id 'java'
    id 'maven'
    id 'signing'
}

group = "com.github.h908714124"
archivesBaseName = "jbock"

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.squareup:javapoet:1.11.1'
    compile 'com.github.h908714124:jbock-annotations:2.2'
    testCompile 'com.google.testing.compile:compile-testing:0.15'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.0-M1'
}


test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

// https://central.sonatype.org/pages/gradle.html
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.properties['ossrhUsername'], password: project.properties['ossrhPassword'])
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: project.properties['ossrhUsername'], password: project.properties['ossrhPassword'])
            }

            pom.project {
                name 'jbock'
                packaging 'jar'
                artifactId 'jbock'
                description 'The jbock annotation processor'
                url 'https://github.com/h908714124/jbock'
            }
        }
    }
}